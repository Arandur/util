Pattern                 :=  Disjunction
                        ;
Disjunction             :=  Alternative
                        |   Alternative, '|', Disjunction
                        ;
Alternative             := 
                        |   Alternative, Term
                        ;
Term                    :=  Atom
                        |   Atom, Quantifier
                        ;
Atom                    :=  Pattern Character
                        |   '.'
                        |   '\', Atom Escape
                        |   Character Class
                        |   '(', Disjunction, ')'
                        |   '(?:', Disjunction, ')'
                        ;
Quantifier              :=  '*'
                        |   '+'
                        |   '?'
                        |   '{', Decimal Integer Literal, '}'
                        |   '{', Decimal Integer Literal, ',', '}'
                        |   '{', Decimal Integer Literal, ',', Decimal Integer Literal, '}'
                        ;
Atom Escape             :=  Decimal Escape
                        |   Character Escape
                        |   Character Class Escape
                        ;
Character Class         :=  '[', Class Ranges, ']'
                        |   '[^', Class Ranges, ']'
                        ;
Decimal Escape          :=  Decimal Integer Literal
                        ;
Character Escape        :=  Control Escape
                        |   'c', Control Letter
                        |   Hex Escape Sequence
                        |   Unicode Escape Sequence
                        |   Identity Escape
                        ;
Character Class Escape  :=  'd' | 'D' | 's' | 'S' | 'w' | 'W'
                        ;
Class Ranges            :=  
                        |   Non Empty Class Ranges
                        ;
Non Empty Class Ranges  :=  Class Atom
                        |   Class Atom, Non Empty Class Ranges No Dash
                        |   Class Atom, '-', Class Atom, Class Ranges
                        ;
Class Atom              :=  '-',
                        |   Class Atom No Dash
                        ;
Non Empty Class Ranges No Dash  :=  Class Atom
                                |   Class Atom No Dash, Non Empty Class Ranges No Dash
                                |   Class Atom No Dash, '-', Class Atom, Class Ranges
                                ;
Class Atom No Dash      :=  Source Character [[ not one of '\', ']', '-' ]]
                        |   '\', Class Escape
                        ;
Class Escape            :=  Decimal Escape
                        |   'b'
                        |   Character Escape
                        |   Character Class Escape
                        ;
Decimal Integer Literal :=  '0'
                        |   Decimal Digit No Zero, Decimal Digits 
                        ;
Decimal Digit No Zero   :=  '1' | '2' | '3' | '4' | '5' | '6' | '7' | '8' | '9'
                        ;
Decimal Digit           :=  '0'
                        |   Decimal Digit No Zero
                        ;
Decimal Digits          :=  Decimal Digit
                        |   Decimal Digit, Decimal Digits
                        ;
Control Escape          :=  'f'
                        |   'n'
                        |   'r'
                        |   't'
                        |   'v'
                        ;
Control Letter          :=  Ascii Letter
                        ;
Hex Escape Sequence     :=  'x', Hex Digit, Hex Digit
                        ;
Unicode Escape Sequence :=  'u', Hex Digit, Hex Digit, Hex Digit, Hex Digit
                        ;
Identity Escape         :=  [[ any non-alphanumeric character ]]
                        ;
Ascii Letter            :=  'a' | 'b' | 'c' | 'd' | 'e' | 'f' | 'g' | 'h' | 'i' | 'j'
                        |   'k' | 'l' | 'm' | 'n' | 'o' | 'p' | 'q' | 'r' | 's' | 't'
                        |   'u' | 'v' | 'w' | 'x' | 'y' | 'z'
                        |   'A' | 'B' | 'C' | 'D' | 'E' | 'F' | 'G' | 'H' | 'I' | 'J'
                        |   'K' | 'L' | 'M' | 'N' | 'O' | 'P' | 'Q' | 'R' | 'S' | 'T'
                        |   'U' | 'V' | 'W' | 'X' | 'Y' | 'Z'
                        ;   
Hex Digit               :=  '0' | '1' | '2' | '3' | '4' | '5' | '6' | '7' | '8' | '9'
                        |   'a' | 'b' | 'c' | 'd' | 'e' | 'f'
                        |   'A' | 'B' | 'C' | 'D' | 'E' | 'F'
                        ;
